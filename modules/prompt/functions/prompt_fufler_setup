#
# A simple theme inspired based on damoekri and nicoulaj themes
#
# Author:
#   Alexey Ermakov <fufler@gmail.com>

# Load dependencies.
pmodload 'helper'

prompt_fufler_precmd() {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  _prompt_fufler_pwd=$(prompt-pwd)

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_fufler_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info and ruby-info before each command.
  add-zsh-hook precmd prompt_fufler_precmd

  local user_char='❯'
  local root_char='❯❯❯'
  local success_color='%F{green}'
  local failure_color='%F{red}'


  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ':%F{magenta}%s%f'
  zstyle ':prezto:module:git:info:branch' format '%F{blue}%b%f'
  zstyle ':prezto:module:git:info:clean' format ' %F{green}✔%f'
  zstyle ':prezto:module:git:info:dirty' format ' %F{red}✗%f'
  zstyle ':prezto:module:git:info:commit' format '%F{blue}%.7c%f'
  zstyle ':prezto:module:git:info:position' format '%F{blue}%p%f'
  zstyle ':prezto:module:git:info:keys' format \
    'rprompt' ' $(coalesce "%b" "%p" "%c")%s%C%D'

  # Define prompts.
  PROMPT='${SSH_TTY:+[%n@%m]}%F{cyan}${_prompt_fufler_pwd}%f${editor_info[keymap]}'" %(?.${success_color}.${failure_color})%<<%(!.${root_char}.${user_char})%b%f "
  RPROMPT='${git_info:+${(e)git_info[rprompt]}}'
}

prompt_fufler_setup "$@"
